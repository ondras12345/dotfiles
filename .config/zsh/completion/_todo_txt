#compdef todo-txt todo.sh

# See http://todotxt.com for todo.sh.
#
# Featurettes:
#  - "replace" will complete the original text for editing
#  - completing priorities will cycle through A to Z (even without
#    menu completion)
#  - list and listall will complete +<project> and @<where> from
#    values in existing entries
#  - will complete after + and @ if typed in message text

setopt localoptions braceccl

local expl curcontext="$curcontext" state line pri nextstate item
local -a cmdlist itemlist match mbegin mend
integer NORMARG

local TODO_SH
if command -v todo-txt &> /dev/null ; then
    TODO_SH='todo-txt'
else
    TODO_SH='todo.sh'
fi

_arguments -s -n : \
  '-@[hide context names]' \
  '-\+[hide project names]' \
  '-c[color mode]' \
  '-d[alternate config file]:config file:_files' \
  '-f[force, no confirmation]' \
  '-h[display help]' \
  '-p[plain mode, no colours]' \
  '-P[hide priority labels]' \
  "-a[don't auto-archive tasks when done]" \
  '-A[auto-archive tasks when done]' \
  '-n[automatically remove blank lines]' \
  '-N[preserve line numbers]' \
  '-t[add current date to task on creation]' \
  "-T[don't add current date to task]" \
  '-v[verbose mode, confirmation messages]' \
  '-vv[extra verbose (debug)]' \
  '-V[display version etc.]' \
  '-x[disable final filter]' \
  '1:command:->commands' \
  '*:arguments:->arguments' && return 0

local projmsg="context or project"
local txtmsg="text with contexts or projects"

# Skip "command" as command prefix if words after
if [[ $words[NORMARG] == command && NORMARG -lt CURRENT ]]; then
  (( NORMARG++ ))
fi

case $state in
  (commands)
  cmdlist=(
    "add:add TODO ITEM to todo.txt."
    "addm:add TODO ITEMs, one per line, to todo.txt."
    "addto:add text to file (not item)"
    "append:adds to item on line NUMBER the text TEXT."
    "archive:moves done items from todo.txt to done.txt."
    "deduplicate:removes duplicate lines from todo.txt."
    "command:run internal commands only"
    "del:deletes the item on line NUMBER in todo.txt."
    "rm:deletes the item on line NUMBER in todo.txt."
    "depri:remove prioritization from item"
    "do:marks item on line NUMBER as done in todo.txt."
    "help:display help"
    "list:displays all todo items containing TERM(s), sorted by priority."
    "listall:displays items including done ones containing TERM(s)"
    "listaddons:lists all added and overridden actions."
    "listcon:list all contexts"
    "listfile:display all files in .todo directory"
    "listpri:displays all items prioritized at PRIORITY."
    "listproj:lists all the projects in todo.txt."
    "move:move item between files"
    "prepend:adds to the beginning of the item on line NUMBER text TEXT."
    "pri:adds or replace in NUMBER the priority PRIORITY (upper case letter)."
    "replace:replace in NUMBER with TEXT."
    "report:adds the number of open and done items to report.txt."
    "due:generates a list of tasks tude today or overdue."
    "schedule:Set, modify or remove t: date (also mv, rm)"
    "revive:Revive todos from done.txt"
    "setdue:Set or change the due date of a set of tasks"
    "tedit:Edit a single task in \$EDITOR"
    "nav:open URL in browser"
    "addt:add TODO ITEM with due:today"
    "top:list as many items as can fit in the terminal"
    "clock:clock in or out of a task in hamster time tracker"
    "note:keep notes attached to tasks"
  )
  _describe -t todo-commands "$TODO_SH command" cmdlist
  ;;

  (arguments)
  case $words[NORMARG] in
    (append|command|del|move|mv|prepend|pri|replace|rm)
    if (( NORMARG == CURRENT - 1 )); then
      nextstate=item
    else
      case $words[NORMARG] in
	(pri)
	nextstate=pri
	;;
	(append|prepend)
	nextstate=proj
	;;
	(move|mv)
	nextstate=file
	;;
	(replace)
	item=${words[CURRENT-1]##0##}
	compadd -Q -- "${(qq)$($TODO_SH -p list "^[ 0]*$item " | sed '/^--/,$d')##<-> (\([A-Z]\) |)}"
	;;
      esac
    fi
    ;;

    (depri|do|dp)
    nextstate=item
    ;;

    (a|add|addm|list|ls|listall|lsa)
    nextstate=proj
    ;;

    (addto)
    if (( NORMARG == CURRENT - 1 )); then
      nextstate=file
    else
      nexstate=proj
    fi
    ;;

    (listfile|lf)
    if (( NORMARG == CURRENT -1 )); then
      nextstate=file
    else
      _message "Term to search file for"
    fi
    ;;

    (listpri|lsp)
    nextstate=pri
    ;;

    (clock)
    if (( NORMARG == CURRENT - 1 )); then
        local -a clock_actions
        clock_actions=(
            "cur:show current running clock"
            "in:start a clock for ITEM"
            "out:end the current running clock"
            "report:show all clock totals for today"
        )
        _describe 'clock action' clock_actions
    else
        case $words[((NORMARG+1))] in
            in|out)
                nextstate=item
                ;;
        esac
    fi
    ;;

    (note)
    if (( NORMARG == CURRENT - 1 )); then
        local -a note_actions
        note_actions=(
            'add:add a new note'
            'edit:edit an existing note'
            'show:show an existing note'
            'rm:remove an existing note'
        )
        _describe 'note action' note_actions
    else
        case $words[((NORMARG+1))] in
            edit|show)
                local -a note_extra=('archive:archived notes')
                _describe 'note archive' note_extra
                nextstate=item
                ;;
            add|rm)
                nextstate=item
                ;;
        esac
    fi
    ;;

    (setdue)
    if (( NORMARG == CURRENT - 1 )); then
        # DUEDATE
        local -a setdue_dates
        setdue_dates=(
            'today'
            'nw:next week (+7)'
            '+1:tomorrow (differential duedate)'
            'mon'
            'tue'
            'wed'
            'thu'
            'fri'
            'sat'
            'sun'
            "$(date -I):full date"
            "--$(date +%m-%d):month and day"
            "---$(date +%d):day"
        )
        _describe 'setdue date' setdue_dates
    else
        nextstate=item
    fi
    ;;

    (schedule)
    if (( NORMARG == CURRENT - 1 )); then
        nextstate=item
    else
        local -a schedule_actions
        schedule_actions=(
            'tomorrow'
            '7 days'  # this escapes spaces, but it does not cause trouble
            'rm:remove'
            "$(date -I):date"
            # mv is obsolete
        )
        _describe 'schedule actions' schedule_actions
    fi
    ;;

    (due)
        if (( NORMARG == CURRENT - 1)) ; then
            local -a due_actions
            due_actions=(
                '0:only today (0 days)'
            )
            _describe 'due actions' due_actions
        fi
        ;;

    (nav)
    nextstate=item
    ;;

    (*)
    return 1
    ;;
  esac
  ;;
esac

case $nextstate in
  (file)
  _path_files -W ~/.todo
  ;;

  (item)
  itemlist=(${${(M)${(f)"$($TODO_SH -p list | sed '/^--/,$d')"}##<-> *}/(#b)(<->) (*)/${match[1]}:${match[2]}})
  _describe -t todo-items 'todo item' itemlist
  ;;

  (pri)
  if [[ $words[CURRENT] = (|[A-Z]) ]]; then
    if [[ $words[CURRENT] = (|Z) ]]; then
      pri=A
    else
      # cycle priority
      pri=$words[CURRENT]
      pri=${(#)$(( #pri + 1 ))}
    fi
    _wanted priority expl 'priority' compadd -U -S '' -- $pri
  else
    _wanted priority expl 'priority' compadd {A-Z}
  fi
  ;;

  (proj)
  # This completes stuff beginning with + (projects) or @ (contexts);
  # these are todo.sh conventions.
  if [[ ! -prefix + && ! -prefix @ ]]; then
    projmsg=$txtmsg
  fi
  # In case there are quotes, ignore anything up to whitespace before
  # the + or @ (which may not even be there yet).
  compset -P '*[[:space:]]'
  _wanted search expl $projmsg \
    compadd $($TODO_SH lsprj) $($TODO_SH lsc)
  ;;
esac
